buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.2'
    }
}

ext {
    scalaVersion = '2.11'
    scalaLibraryVersion = '2.11.8'
}

apply plugin: 'scala'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'cz.augi.gsonscala'
archivesBaseName = "gson-scala_$scalaVersion"
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

repositories {
    jcenter()
}

dependencies {
    compile "org.scala-lang:scala-library:${scalaLibraryVersion}"
	compile 'com.google.code.gson:gson:2.8.0'
    testCompile "org.scalatest:scalatest_${scalaVersion}:3.0.0"
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = "gson-scala_$scalaVersion"
        desc = 'Allows to use Scala and Java 8 types with gson library.'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/augi/gson-scala.git'
        websiteUrl = 'https://github.com/augi/gson-scala'
        issueTrackerUrl = 'https://github.com/augi/gson-scala/issues'
        labels = ['gson', 'scala', 'json']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}
