buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    }
}

apply plugin: 'scala'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'cz.augi'
archivesBaseName = 'gson-scala'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

repositories {
    jcenter()
}

ext {
  scalaVersion = '2.11'
  scalaLibraryVersion = '2.11.8'
}

dependencies {
    compile "org.scala-lang:scala-library:${scalaLibraryVersion}"
	compile 'com.google.code.gson:gson:2.3.1'
    testCompile "org.scalatest:scalatest_${scalaVersion}:2.2.6"
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'gson-scala'
        desc = 'Allows to use Scala and Java 8 types with gson library.'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/augi/gson-scala.git'
        websiteUrl = 'https://github.com/Augi/gson-scala'
        issueTrackerUrl = 'https://github.com/Augi/gson-scala/issues'
        labels = ['gson', 'scala', 'json']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}
